#!/usr/bin/gnuplot
#
# Script        :swap.gplot
# Author        :Julio Sanz
# Website       :www.elarraydejota.com
# Email         :juliojosesb@gmail.com
# Description   :Script to create a SWAP usage graph with gnuplot
# Dependencies  :gnuplot
# Usage         :gnuplot swap.gplot
# License       :GPLv3
#

reset

# Terminal config
set terminal pngcairo enhanced font 'Verdana,8'
set output '../../graphs/swap.png'
set title "SWAP usage graph"
set key bmargin

# Lines
set style line 1 lc rgb '#e74c3c' pt 1 ps 1 lt 1 lw 2 # line1
set style line 2 lc rgb '#3498db' pt 6 ps 1 lt 1 lw 2 # line2

# Axis configuration
set style line 11 lc rgb '#2c3e50' lt 1 lw 1.5 # Axis line
set border 3 back ls 11
set tics nomirror
set autoscale xy
set xdata time
set timefmt "%H:%M:%S"
set xlabel "Time"
set ylabel "SWAP (MB)"

# Background grid
set style line 11 lc rgb '#aeb6bf' lt 0 lw 2
set grid back ls 11

# Begin plotting
plot '../../data/swap.dat' using 1:($2/1024) title 'Free' with l ls 1, \
     ''                    using 1:($3/1024) title 'Used' with l ls 2, \
