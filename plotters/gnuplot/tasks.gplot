#!/usr/bin/gnuplot
#
# Script        :tasks.gplot
# Author        :Julio Sanz
# Website       :www.elarraydejota.com
# Email         :juliojosesb@gmail.com
# Description   :Script to create tasks graph with gnuplot
# Dependencies  :gnuplot
# Usage         :gnuplot tasks.gplot
# License       :GPLv3
#

reset

set terminal pngcairo enhanced font 'Verdana,8'
set output '../../graphs/tasks.png'
set title "Tasks graph"
set key bmargin

# Styles for different lines
set style line 1 lc rgb '#e74c3c' pt 1 ps 1 lt 1 lw 2 # line1
set style line 2 lc rgb '#3498db' pt 6 ps 1 lt 1 lw 2 # line2
set style line 3 lc rgb '#28b463' pt 4 ps 1 lt 1 lw 2 # line3

# Axis configuration
set style line 11 lc rgb '#2c3e50' lt 1 lw 1.5 # Axis line
set border 3 back ls 11
set tics nomirror
set autoscale xy
set xdata time
set timefmt "%H:%M:%S"
set xlabel "Time"
set ylabel "Tasks"

# Background grid
set style line 11 lc rgb '#aeb6bf' lt 0 lw 2
set grid back ls 11

# Begin plotting
plot '../../data/loadaverage.dat' using 1:2 title 'Tasks in run queue' with l ls 1, \
     ''                           using 1:3 title 'Total active tasks (processes + threads)' with l ls 2, \
     ''                           using 1:7 title 'Blocked tasks' with l ls 3
